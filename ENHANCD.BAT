@ECHO OFF

IF NOT "%~1" == "" IF NOT "%~1" == "." IF NOT "%~1" == "..." (
	CD %* >NUL 2>&1
	IF NOT ERRORLEVEL 1 GOTO :AFTER_CD
)

IF "%~1" == "." (
	SET ENHANCD_CANCELLED=1
	FOR /F "delims=" %%I IN ('DIR /AD /B ^| "%ENHANCD_FZY%"') DO (
		SET ENHANCD_CANCELLED=
		CD /D %%I
	)
	IF NOT DEFINED ENHANCD_CANCELLED GOTO :AFTER_CD
	SET ENHANCD_CANCELLED=
	EXIT /B
)

IF "%~1" == "..." (
	CALL :LIST_PARENTS > "%ENHANCD_LOG%.tmp"
	SET ENHANCD_CANCELLED=1
	FOR /F "delims=" %%I IN ('""%ENHANCD_FZY:)=^)%" < "%ENHANCD_LOG%.tmp""') DO (
		SET ENHANCD_CANCELLED=
		CD /D %%I
	)
	IF NOT DEFINED ENHANCD_CANCELLED GOTO :AFTER_CD
	SET ENHANCD_CANCELLED=
	EXIT /B
)

IF "%~1" == "-" (
	CALL :LIST_LOG_EXCEPT_CD %ENHANCD_HYPHEN_NUM% > "%ENHANCD_LOG%.tmp"
	SET ENHANCD_CANCELLED=1
	FOR /F "delims=" %%I IN ('""%ENHANCD_FZY:)=^)%" < "%ENHANCD_LOG%.tmp""') DO (
		SET ENHANCD_CANCELLED=
		CD /D %%I
	)
	IF NOT DEFINED ENHANCD_CANCELLED GOTO :AFTER_CD
	SET ENHANCD_CANCELLED=
	EXIT /B
)

IF "%~1" == "" (
	CALL :LIST_LOG_EXCEPT_CD > "%ENHANCD_LOG%.tmp"
	SET ENHANCD_CANCELLED=1
	FOR /F "delims=" %%I IN ('""%ENHANCD_FZY:)=^)%" < "%ENHANCD_LOG%.tmp""') DO (
		SET ENHANCD_CANCELLED=
		CD /D %%I
	)
	IF NOT DEFINED ENHANCD_CANCELLED GOTO :AFTER_CD
	SET ENHANCD_CANCELLED=
	EXIT /B
)

REM Filter logs with given argument.
(
	CALL :LIST_LOG_EXCEPT_CD > "%ENHANCD_LOG%.tmp"
	SET ENHANCD_CANCELLED=1
	FOR /F "delims=" %%I IN ('""%ENHANCD_FZY:)=^)%" -e "%*" < "%ENHANCD_LOG%.tmp" | "%ENHANCD_FZY:)=^)%""') DO (
		SET ENHANCD_CANCELLED=
		CD /D %%I
	)
	IF NOT DEFINED ENHANCD_CANCELLED GOTO :AFTER_CD
	SET ENHANCD_CANCELLED=
	EXIT /B
)

:AFTER_CD
(
	CD
	ECHO %USERPROFILE%
	TYPE "%ENHANCD_LOG%"
	FOR /D %%I IN (*) DO ECHO %%~fI
	CALL :LIST_PARENTS
) > "%ENHANCD_LOG%.tmp"
CALL :UNIQUE "%ENHANCD_LOG%.tmp" > "%ENHANCD_LOG%"

EXIT /B


:LIST_LOG_EXCEPT_CD
	SETLOCAL ENABLEDELAYEDEXPANSION

	IF "%__CD__:~3%" == "" (
		SET STRING=!__CD__!
	) ELSE (
		SET STRING=!__CD__:~0,-1!
	)
	SET STRING=%STRING:\=\\%

	IF "%1" == "" (
		FOR /F "delims=" %%I IN ('FINDSTR /X /V /C:"%STRING%" "%ENHANCD_LOG%"') DO (
			IF EXIST %%I ECHO %%I
		)
	) ELSE (
		SET LIMIT=%1
		FOR /F "delims=" %%I IN ('FINDSTR /X /V /C:"%STRING%" /C:"%USERPROFILE%" "%ENHANCD_LOG%"') DO (
			IF "!LIMIT!" == "0" GOTO :EOF
			IF EXIST %%I (
				ECHO %%I
				SET /A LIMIT=!LIMIT! - 1
			)
		)
	)

	ENDLOCAL
	EXIT /B


:LIST_PARENTS
	SETLOCAL

	:LIST_PARENTS_LOOP
	CD ..>NUL 2>&1
	CD
	IF NOT "%__CD__:~3%" == "" GOTO :LIST_PARENTS_LOOP

	ENDLOCAL
	EXIT /B


:UNIQUE
	IF DEFINED ENHANCD_AWK (
		"%ENHANCD_AWK%" -v ORS="\r\n" "!x[$0]++" "%~1"
		EXIT /B
	)

	SETLOCAL
	SET FILENAME=%~1
	SET FILENAME=%FILENAME:'=''%

	START /B powershell.exe -Command "$foundLines = @{}; Get-Content '%FILENAME%' | %% { if (!$foundLines.ContainsKey($_)) { $foundLines.Add($_, $True); $_ }}"

	ENDLOCAL
	EXIT /B

